/*
 * Copyright 2015-2018 Canoo Engineering AG.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext.depProjects = [
    ':dolphin-platform-core',
    ':dolphin-platform-remoting-common',
    ':dolphin-platform-client',
    ':dolphin-platform-remoting-client'
]

apply from: rootProject.file('gradle/code-coverage.gradle')

dependencies {
    testCompile project(':dolphin-platform-remoting-client')
    testCompile "org.testng:testng:$testngVersion"
    testCompile "org.slf4j:slf4j-simple:$slf4jVersion"
    testCompile project(':integration-common')
}

task copyWar(dependsOn: [':integration-tests-jee:war']) {
    doLast {
        copy {
            into file("docker-base/payara-integration")
            from(file("../integration-tests-jee/build/libs/integration-tests.war"))
        }
        copy {
            into file("docker-base/tomee-integration")
            from(file("../integration-tests-jee/build/libs/integration-tests.war"))
        }
        copy {
            into file("docker-base/wildfly-integration")
            from(file("../integration-tests-jee/build/libs/integration-tests.war"))
        }
    }
}

task dockerComposeDown(type: Exec) {
    commandLine "docker-compose", "kill"
}

task dockerComposeUp(type: Exec) {
    commandLine "docker-compose", "up", "-d", "--build", "--force-recreate"
}
dockerComposeUp.dependsOn(copyWar)
test.dependsOn dockerComposeUp
test.finalizedBy(dockerComposeDown)
